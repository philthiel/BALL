FOREACH(i @TOOLS_EXECUTABLES@)
	FILE(COPY @CMAKE_RUNTIME_OUTPUT_DIRECTORY@/TOOLS/${i}
		   DESTINATION @BALLAXY_TOOLS_DIR@/bin)
ENDFOREACH()

FILE(MAKE_DIRECTORY @BALLAXY_TOOLS_DIR@/config)

SET(ENV{BALL_DATA_PATH} @CMAKE_SOURCE_DIR@/data)
SET(XML_FILES)
SET(ENV{PYTHONPATH} @BALL_PYTHONPATH@)
FOREACH(BALLAXY_TOOL @TOOLS_EXECUTABLES@)
	# generate the CTD	
	EXECUTE_PROCESS(COMMAND @BALLAXY_TOOLS_DIR@/bin/${BALLAXY_TOOL} -write_par @BALLAXY_TOOLS_DIR@/config/${BALLAXY_TOOL}.ctd OUTPUT_QUIET ERROR_QUIET)
	# convert it using CTD2Galaxy
	MESSAGE(STATUS "Converting ${BALLAXY_TOOL}.ctd -> ${BALLAXY_TOOL}.xml")
	EXECUTE_PROCESS(
		COMMAND @PYTHON_EXECUTABLE@ @CTD2GALAXY@ --input @BALLAXY_TOOLS_DIR@/config/${BALLAXY_TOOL}.ctd --output-destination @BALLAXY_TOOLS_DIR@/config/${BALLAXY_TOOL}.xml --blacklist help h write_par par env write_ini --formats-file @CTD2GALAXY_FORMATS_FILE@ --macros @CTD2GALAXY_MACROS_FILE@ RESULT_VARIABLE exit_code  OUTPUT_VARIABLE stdout ERROR_VARIABLE stderr OUTPUT_STRIP_TRAILING_WHITESPACE ERROR_STRIP_TRAILING_WHITESPACE)
	IF (NOT (exit_code EQUAL 0))
		MESSAGE(WARNING "CTD2Galaxy reported an error while generating the ToolConfig for ${BALLAXY_TOOL}\nExit code: ${exit_code}\nStderr: ${stderr}\nStdout: ${stdout}")
	ELSE()
		LIST(APPEND XML_FILES "@BALLAXY_TOOLS_DIR@/config/${BALLAXY_TOOL}.xml")
	ENDIF()
	# remove the ctd temp file
	FILE(REMOVE @BALLAXY_TOOLS_DIR@/config/${BALLAXY_TOOL}.ctd)
ENDFOREACH()

SET(TOOL_CATEGORIES)

FOREACH(i ${XML_FILES})
	FILE(READ ${i} XML_CONTENT)
	STRING(REGEX MATCH "[ \t]*<!--Proposed Tool Section: [^>]*" result "${XML_CONTENT}")
	STRING(REGEX REPLACE ".*\\[(.*)\\].*" \\1 result ${result})
	IF (NOT result)
		SET(result "DEFAULT")
	ENDIF()
	LIST(APPEND ${result}_TOOL_LIST ${i})
	LIST(APPEND TOOL_CATEGORIES ${result})
ENDFOREACH()

SET(TOOL_DEFINITION)
LIST(REMOVE_DUPLICATES TOOL_CATEGORIES) 
FOREACH(category ${TOOL_CATEGORIES})
	SET(TOOL_DEFINITION "${TOOL_DEFINITION} \t<section name=\"${category}\" id=\"${category}\">")
	FOREACH(tool ${${category}_TOOL_LIST})
		GET_FILENAME_COMPONENT(tool_basename ${tool} NAME)
		SET(TOOL_DEFINITION "${TOOL_DEFINITION}\n\t\t<tool file=\"BALL/${tool_basename}\"/>")
	ENDFOREACH()
	SET(TOOL_DEFINITION "${TOOL_DEFINITION}\n\t</section>\n\n")
ENDFOREACH()

FILE(WRITE "@BALLAXY_TOOLS_DIR@/tool_conf.xml.section" ${TOOL_DEFINITION})

